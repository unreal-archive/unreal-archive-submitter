plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'jacoco'

	alias(libs.plugins.jlink)
	alias(libs.plugins.extra.module.info)
}

group = 'org.unrealarchive'
version = "1.11"
if (System.getenv().containsKey("BUILD_NUMBER")) {
	version += ".${System.getenv("BUILD_NUMBER")}"
} else version += ".DEV"

application {
	mainClass = 'org.unrealarchive.submitter.Main'
	mainModule = 'unreal.archive.submit'
}

jar {
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': project.version,
				'Main-Class': application.mainClass,
		)
	}
}

compileJava {
    options.release = 21
}

jlink {
	mergedModuleName = "unreal.archive.submit.merged"
	options = ['--compress', 'zip-6', '--no-header-files', '--no-man-pages']

	imageName = "${project.name}"
	imageDir = layout.buildDirectory.dir(project.name)

	forceMerge 'slf4j'

	jpackage {
		installerType = project.findProperty('installerType')
	}
}

def tarTask = tasks.register('jlinkTar', Tar) {
	dependsOn jlinkZip
	archiveFileName = "${jlink.imageName.get()}.tgz"
	destinationDirectory = layout.buildDirectory
	compression = Compression.GZIP

	into("${jlink.imageName.get()}") {
		from jlink.imageDir
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		url = "https://git.pookaroo.au/api/packages/shrimp/maven"
		credentials(HttpHeaderCredentials) {
			name = "Authorization"
			value = "token ${providers.gradleProperty("packagesReadToken").get()}"
		}
		authentication {
			header(HttpHeaderAuthentication)
		}
	}
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://git.pookaroo.au/api/packages/shrimp/maven'
			credentials(HttpHeaderCredentials) {
				name = "Authorization"
				value = "token ${System.env.PACKAGES_TOKEN}"
			}

			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
			artifact(tarTask) {
				classifier = 'bin'
				extension = 'tgz'
			}
		}
	}
}

artifacts {
	archives(layout.buildDirectory.file("${project.name}-bin.tgz")) {
		type = 'tgz'
		builtBy tarTask
	}
}

dependencies {
	implementation(libs.bundles.unreal.archive)

	implementation(libs.bundles.slf4j)
	implementation(libs.undertow.core)
	implementation(libs.jgit)
	implementation(libs.github.api)

	implementation(libs.jackson.databind)

	testImplementation(libs.junit.jupiter)
	testImplementation(libs.mockito.core)

	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

extraJavaModuleInfo {
	failOnMissingModuleInfo.set(false)

	module("org.kohsuke:github-api", "org.kohsuke.github.api") {
		requires("java.logging")
		requires("com.fasterxml.jackson.databind")
		requires("org.apache.commons.lang3")
		requires("org.apache.commons.io")
		exports("org.kohsuke.github")
	}

	module("org.jboss.xnio:xnio-api", "xnio.api") {
		exports("org.xnio")
	}

	module("io.undertow:undertow-core", "undertow.core") {
		requires("xnio.api")

		exports("io.undertow")
		exports("io.undertow.io")
		exports("io.undertow.server")
		exports("io.undertow.server.handlers.form")
		exports("io.undertow.util")
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
