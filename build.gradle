plugins {
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'jacoco'

	id 'org.gradlex.extra-java-module-info' version '1.8'
	id 'org.beryx.jlink' version '2.26.0'
}

group = 'org.unrealarchive'
version = "1.7"
if (System.getenv().containsKey("BUILD_NUMBER")) {
	version += ".${System.env.BUILD_NUMBER}"
} else version += ".DEV"

application {
	mainClassName = 'org.unrealarchive.submitter.Main'
	mainModule = 'unreal.archive.submit'
}

compileJava {
    options.release = 17
}

jlink {
	mergedModuleName = "unreal.archive.submit.merged"
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

	imageName = "${project.name}"
	imageDir = layout.buildDirectory.dir(project.name)

	jpackage {
		installerType = project.findProperty('installerType')
	}
}

repositories {
	mavenCentral()
	mavenLocal()
	maven {
		url "https://code.shrimpworks.za.net/artefacts"
		matching {
			group "org.unrealarchive"
			group "net.shrimpworks"
		}
	}
}

def tarBinary = artifacts.add('archives', layout.buildDirectory.file("${project.name}.tgz").get().asFile) {
	type 'tgz'
	builtBy 'jlinkTar'
}

tasks.register('jlinkTar', Tar) {
	dependsOn jlinkZip
	archiveFileName = "${jlink.imageName.get()}.tgz"
	destinationDirectory = layout.buildDirectory
	compression = Compression.GZIP

	into("${jlink.imageName.get()}") {
		from jlink.imageDir
	}
}

publishing {
	repositories {
		maven {
			name = "ShrimpWorks"
			url = 'https://code.shrimpworks.za.net/artefacts/'
			authentication {
				basic(BasicAuthentication)
			}
			credentials(PasswordCredentials) {
				username = "${System.env.ARTEFACTS_USER}"
				password = "${System.env.ARTEFACTS_PASSWORD}"
			}
		}
	}
	publications {
		maven(MavenPublication) {
			from components.java
		}
		mavenLatest(MavenPublication) {
			pom {
				version = "latest"
			}
			artifact tarBinary
		}
	}
}

dependencies {
	implementation 'org.unrealarchive:common:1.15.336'
	implementation 'org.unrealarchive:content:1.15.336'
	implementation 'org.unrealarchive:storage:1.15.336'
	implementation 'org.unrealarchive:unreal-archive:1.15.336'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'

	implementation 'org.slf4j:slf4j-api:2.0.12'
	implementation 'org.slf4j:slf4j-simple:2.0.12'

	implementation 'io.undertow:undertow-core:2.3.12.Final'

	implementation 'org.eclipse.jgit:org.eclipse.jgit:6.9.0.202403050737-r'
	implementation 'org.kohsuke:github-api:1.321'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
	testImplementation 'org.mockito:mockito-core:5.2.0'
}

extraJavaModuleInfo {
	failOnMissingModuleInfo.set(false)

	module("org.kohsuke:github-api", "org.kohsuke.github.api") {
		requires("java.logging")
		requires("com.fasterxml.jackson.databind")
		requires("org.apache.commons.lang3")
		requires("org.apache.commons.io")
		exports("org.kohsuke.github")
	}

	module("org.eclipse.jgit:org.eclipse.jgit", "org.eclipse.jgit") {
		requires("org.slf4j")
		exports("org.eclipse.jgit.lib")
		exports("org.eclipse.jgit.transport")
		exports("org.eclipse.jgit.api")
		exports("org.eclipse.jgit.api.errors")
	}

	module("org.jboss.xnio:xnio-api", "xnio.api") {
		exports("org.xnio")
	}

	module("io.undertow:undertow-core", "undertow.core") {
		requires("xnio.api")

		exports("io.undertow")
		exports("io.undertow.io")
		exports("io.undertow.server")
		exports("io.undertow.server.handlers.form")
		exports("io.undertow.util")
	}
}

jar {
	manifest {
		attributes(
				'Implementation-Title': project.name,
				'Implementation-Version': project.version,
				'Main-Class': mainClassName,
		)
	}
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
